c-----------------------------------------------------------------
c
c       CIMMS/CSU LES - Version X 
c
c       (C) 1994-1998, Marat Khairoutdinov 
c-----------------------------------------------------------------
c     positively definite monotonic advection with non-oscillatory option
c     and gravitational sedimentation (rain and snow advection)
c-----------------------------------------------------------------


 	subroutine precip_fall()
 	
 	implicit none
	include 'common.inc'
	include 'params.inc'

	real mx(nx,ny,nzm),mn(nx,ny,nzm)
	real www(nx,ny,nz),fz(nx,ny,nz)
	real f0(nzm)
	real eps
	integer i,j,k,kc,kb
	logical nonos

	real x1, x2, a, b, a1, a2, y, irho,lstar
	real*8 rhoz
	real vrain, vsnow, vgrau, crain, csnow, cgrau
        real lfac(nx,ny,nz), qrr, qss, qgg, omp, omg
	real donor,andiff,pp,pn, lat_heat

	andiff(x1,x2,a,b)=(abs(a)-a*a*b)*0.5*(x2-x1)
	pp(y)= max(0.,y)
	pn(y)=-min(0.,y)

c--------------------------------------------------------

	eps = 1.e-10
	nonos = .true.
  
	 crain = b_rain / 4.
	 csnow = b_snow / 4. 
	 cgrau = b_grau / 4. 
	 vrain = a_rain * gamr3 / 6. / (pi * rhor * nzeror) ** crain	  
	 vsnow = a_snow * gams3 / 6. / (pi * rhos * nzeros) ** csnow
	 vgrau = a_grau * gamg3 / 6. / (pi * rhog * nzerog) ** cgrau
c
c 	Add sedimentation of precipitation field to the vert. vel.
c
	 do k=1,nzm
	  rhoz = rhow(k)*dtn/dz*sqrt(1.29/rho(k)) 
c	  rhoz = rhow(k)*dtn/dz*(1000./pres(k))**0.4 
	  do j=1,ny
	   do i=1,nx
	     www(i,j,k)=0.
             omp = omegap(i,j,k)
             lfac(i,j,k) = fac_cond+(1.-omp)*fac_fus
	     if(qp(i,j,k).gt.qp_threshold) then
	        omp = omegap(i,j,k)
	        if(omp.eq.1.) then
	          www(i,j,k)= vrain*(rho(k)*qp(i,j,k))**crain
	        elseif(omp.eq.0.) then
	          omg = omegag(i,j,k)
	          qgg=omg*qp(i,j,k)
	          qss=qp(i,j,k)-qgg
	          www(i,j,k)=
     &                 omg*vgrau*(rho(k)*qgg)**cgrau+
     &             (1.-omg)*vsnow*(rho(k)*qss)**csnow
	        else
	          omg = omegag(i,j,k)
	          qrr=omp*qp(i,j,k)
	          qss=qp(i,j,k)-qrr
	          qgg=omg*qss
	          qss=qss-qgg
	          www(i,j,k)=omp*vrain*(rho(k)*qrr)**crain+
     &		   (1.-omp)*(omg*vgrau*(rho(k)*qgg)**cgrau+
     &             (1.-omg)*vsnow*(rho(k)*qss)**csnow)
	        endif
	        www(i,j,k) = min(0.9*dz*adz(k)/dtn, www(i,j,k)) 
                www(i,j,k) = -www(i,j,k)*rhoz
	     endif 	         
	   end do
	  end do
	 end do	
	 
	 do j=1,ny
	  do i=1,nx 
	   fz(i,j,nz)=0.
	   www(i,j,nz)=0.
	   lfac(i,j,nz)=0.
	  end do
	 end do

c-----------------------------------------
	 	 
	if(nonos) then

	 do k=1,nzm
          kc=min(nzm,k+1)
	  kb=max(1,k-1)
	  do j=1,ny
	   do i=1,nx
	     mx(i,j,k)=max(qp(i,j,kb),qp(i,j,kc),qp(i,j,k))
 	     mn(i,j,k)=min(qp(i,j,kb),qp(i,j,kc),qp(i,j,k))	  
	   end do
	  end do
	 end do

	 
	end if  ! nonos

c-------------------------------------
c	loop over iterations
c-----------------------------------------
	 do k=1,nzm
	  kb=max(1,k-1)
	  do j=1,ny
	   do i=1,nx
  	    fz(i,j,k)=qp(i,j,k)*www(i,j,k)
            precflux(k) = precflux(k) - fz(i,j,k)
	   end do
	  end do
	 end do


	 do j=1,ny
	  do i=1,nx
	    precsfc(i,j) = precsfc(i,j) - fz(i,j,1)
	    precssfc(i,j) = precssfc(i,j) - fz(i,j,1)*(1.-omegap(i,j,1))
	  end do
	 end do


	 do k=1,nzm
	  kc=k+1
	  rhoz = 1./(rho(k)*adz(k))
	  do j=1,ny
	   do i=1,nx
 	    qp(i,j,k)=qp(i,j,k)-(fz(i,j,kc)-fz(i,j,k))*rhoz
 	   end do
	  end do
	 end do 

	 do k=1,nzm
	  kb=max(1,k-1)
	  irho=1./(rho(k)*adz(k))
	  do j=1,ny
	   do i=1,nx
 	    www(i,j,k)=andiff(qp(i,j,kb),qp(i,j,k),www(i,j,k),irho)
	   end do
	 end do
	end do

c---------- non-osscilatory option ---------------

	if(nonos) then

	 do k=1,nzm
          kc=min(nzm,k+1)
	  kb=max(1,k-1)
	  do j=1,ny
	   do i=1,nx
	     mx(i,j,k)=max(qp(i,j,kb),qp(i,j,kc),qp(i,j,k),mx(i,j,k))
 	     mn(i,j,k)=min(qp(i,j,kb),qp(i,j,kc),qp(i,j,k),mn(i,j,k))	  
	   end do
	  end do
	 end do

	 do k=1,nzm
          kc=min(nzm,k+1)
	  rhoz = rho(k)*adz(k)
	  do j=1,ny
	   do i=1,nx
	     mx(i,j,k)=rhoz*(mx(i,j,k)-qp(i,j,k))/
     $           (pn(www(i,j,kc)) + pp(www(i,j,k))+eps)	
	     mn(i,j,k)=rhoz*(qp(i,j,k)-mn(i,j,k))/
     $           (pp(www(i,j,kc)) + pn(www(i,j,k))+eps)	
	   end do
	  end do
	 end do

	 do k=1,nzm
	  kb=max(1,k-1)
	  do j=1,ny
	   do i=1,nx
 	     www(i,j,k)=pp(www(i,j,k))*min(1.,mx(i,j,k), mn(i,j,kb))
     $           - pn(www(i,j,k))*min(1.,mx(i,j,kb),mn(i,j,k))
	     precflux(k) = precflux(k) - www(i,j,k)
	   end do
	  end do
	 end do

	 do j=1,ny
	  do i=1,nx
	    precsfc(i,j) = precsfc(i,j) - www(i,j,1)
	    precssfc(i,j) = precssfc(i,j) - www(i,j,1)*(1.-omegap(i,j,1))
	  end do
	 end do

	endif ! nonos

	 do k=1,nzm
	  kc=k+1
	  rhoz = 1./(rho(k)*adz(k))
	  do j=1,ny
	   do i=1,nx
 	    qp(i,j,k)=qp(i,j,k)-(www(i,j,kc)-www(i,j,k))*rhoz
            qpfall(k)=qpfall(k)-(www(i,j,kc)+fz(i,j,kc) 
     &                 -www(i,j,k) -fz(i,j,k))*rhoz
            lat_heat = -(lfac(i,j,kc)*(www(i,j,kc)+fz(i,j,kc)) 
     &             - lfac(i,j,k)*(www(i,j,k)+fz(i,j,k)))*rhoz
            t(i,j,k)=t(i,j,k)-lat_heat
 	   end do
	  end do
	 end do 


	return
	end


